---
layout: default
title: è®©æˆ‘ä»¬å¼€å§‹å§
nav_order: 1
description: "Arduino Simple Field Oriented Control (FOC) library ."
permalink: /example_from_scratch
parent: ç¼–å†™ä»£ç 
grand_parent: Arduino <span class="simple">Simple<span class="foc">FOC</span>library</span> 
toc: true
---


#  è®©æˆ‘ä»¬å¼€å§‹å§

ä¸€æ—¦ä½ å®‰è£…å¥½äº† <span class="simple">Simple<span class="foc">FOC</span>library</span> [åº“](installation)ï¼Œå¹¶ä¸”æ‹¥æœ‰äº†æ‰€æœ‰å¿…è¦çš„ [ç¡¬ä»¶](supported_hardware)ï¼Œæˆ‘ä»¬ç»ˆäºå¯ä»¥å¼€å§‹æœ‰è¶£çš„éƒ¨åˆ†äº†â€”â€”ç¼–å†™ä»£ç å¹¶è®©ç”µæœºè½¬åŠ¨èµ·æ¥ï¼

## æ­¥éª¤ 1. æµ‹è¯•ä¼ æ„Ÿå™¨
ä¸€åˆ‡è¿æ¥æ­£å¸¸çš„ç¬¬ä¸€ä¸ªè¿¹è±¡æ˜¯ä¼ æ„Ÿå™¨è¯»æ•°æ­£å¸¸ã€‚è¦æµ‹è¯•ä¼ æ„Ÿå™¨ï¼Œè¯·æµè§ˆåº“ç¤ºä¾‹ `examples/utils/sensor_test` å¹¶æ‰¾åˆ°é€‚åˆä½ çš„ä¼ æ„Ÿå™¨çš„ç¤ºä¾‹ã€‚
ç¤ºä¾‹çš„ç»“æ„å¤§è‡´å¦‚ä¸‹ï¼š



<a href="javascript:show('enc','sensor');" class="btn btn-sensor btn-enc btn-primary">Encoder</a> 
<a href ="javascript:show('mspi','sensor');"  class="btn btn-sensor btn-mspi">Magnetic sensor SPI</a>
<a href ="javascript:show('mi2c','sensor');"  class="btn btn-sensor btn-mi2c">Magnetic sensor I2C</a>
<a href ="javascript:show('manalog','sensor');"  class="btn btn-sensor btn-manalog">Magnetic sensor Analog</a>
<a href ="javascript:show('hall','sensor');"  class="btn btn-sensor btn-hall">Hall sensors</a>


<div class="sensor sensor-manalog hide"  markdown="1">

```cpp
#include <SimpleFOC.h>

// MagneticSensorAnalog(uint8_t _pinAnalog, int _min, int _max)
// - pinAnalog      - the pin that is reading the pwm from magnetic sensor
// - min_raw_count  - the smallest expected reading. 
// - max_raw_count  - the largest value read. 
MagneticSensorAnalog sensor = MagneticSensorAnalog(A1, 14, 1020);

void setup() {
  // monitoring port
  Serial.begin(115200);

  // initialise magnetic sensor hardware
  sensor.init();

  Serial.println("Sensor ready");
  _delay(1000);
}

void loop() {
  // iterative function updating the sensor internal variables
  // it is usually called in motor.loopFOC()
  // this function reads the sensor hardware and 
  // has to be called before getAngle nad getVelocity
  sensor.update();
  // display the angle and the angular velocity to the terminal
  Serial.print(sensor.getAngle());
  Serial.print("\t");
  Serial.println(sensor.getVelocity());
}
```

</div>


<div class="sensor sensor-mi2c hide"  markdown="1">

```cpp
#include <SimpleFOC.h>

// Example of AS5600 configuration 
MagneticSensorI2C sensor = MagneticSensorI2C(AS5600_I2C);

void setup() {
  // monitoring port
  Serial.begin(115200);

  // configure i2C
  Wire.setClock(400000);
  // initialise magnetic sensor hardware
  sensor.init();

  Serial.println("Sensor ready");
  _delay(1000);
}

void loop() {
  // iterative function updating the sensor internal variables
  // it is usually called in motor.loopFOC()
  // this function reads the sensor hardware and 
  // has to be called before getAngle nad getVelocity
  sensor.update();
  
  // display the angle and the angular velocity to the terminal
  Serial.print(sensor.getAngle());
  Serial.print("\t");
  Serial.println(sensor.getVelocity());
}
```

</div>


<div class="sensor sensor-mspi  hide"  markdown="1">

```cpp
#include <SimpleFOC.h>

// MagneticSensorSPI(MagneticSensorSPIConfig_s config, int cs)
//  config  - SPI config
//  cs      - SPI chip select pin 
MagneticSensorSPI sensor = MagneticSensorSPI(AS5147_SPI, 10);

void setup() {
  // monitoring port
  Serial.begin(115200);

  // initialise magnetic sensor hardware
  sensor.init();

  Serial.println("Sensor ready");
  _delay(1000);
}

void loop() {
  // iterative function updating the sensor internal variables
  // it is usually called in motor.loopFOC()
  // this function reads the sensor hardware and 
  // has to be called before getAngle nad getVelocity
  sensor.update();
  // display the angle and the angular velocity to the terminal
  Serial.print(sensor.getAngle());
  Serial.print("\t");
  Serial.println(sensor.getVelocity());
}
```

</div>


<div class="sensor sensor-hall  hide"  markdown="1">

```cpp
#include <SimpleFOC.h>

// Hall sensor instance
// HallSensor(int hallA, int hallB , int cpr, int index)
//  - hallA, hallB, hallC    - HallSensor A, B and C pins
//  - pp                     - pole pairs
HallSensor sensor = HallSensor(2, 3, 4, 14);

void setup() {
  // monitoring port
  Serial.begin(115200);
  
  // initialise sensor hardware
  sensor.init();

  Serial.println("Sensor ready");
  _delay(1000);
}

void loop() {
  // iterative function updating the sensor internal variables
  // it is usually called in motor.loopFOC()
  sensor.update();
  // display the angle and the angular velocity to the terminal
  Serial.print(sensor.getAngle());
  Serial.print("\t");
  Serial.println(sensor.getVelocity());
  delay(100);
}
```

</div>


<div class="sensor sensor-enc"  markdown="1">

```cpp
#include <SimpleFOC.h>

Encoder encoder = Encoder(2, 3, 500);
// interrupt routine initialisation
void doA(){encoder.handleA();}
void doB(){encoder.handleB();}

void setup() {
  // monitoring port
  Serial.begin(115200);
  
  // initialise encoder hardware
  encoder.init();
  // hardware interrupt enable
  encoder.enableInterrupts(doA, doB);

  Serial.println("Encoder ready");
  _delay(1000);
}

void loop() {
  // IMPORTANT
  // read sensor and update the internal variables
  encoder.update();
  // display the angle and the angular velocity to the terminal
  Serial.print(encoder.getAngle());
  Serial.print("\t");
  Serial.println(sensor.getVelocity());
}
```

</div>

è¯·åŠ¡å¿…æ ¹æ®ä½ çš„åº”ç”¨ä¿®æ”¹ä¼ æ„Ÿå™¨å‚æ•°ï¼Œä¾‹å¦‚å¼•è„šå·ã€æ¯è½¬è„‰å†²æ•°ã€æ€»çº¿åœ°å€ç­‰ã€‚å¦‚æœä½ å¯¹æŸäº›å‚æ•°ä¸ç¡®å®šï¼Œè¯·æŸ¥é˜… [ä¼ æ„Ÿå™¨æ–‡æ¡£](sensors)ã€‚


å¦‚æœä½ çš„ä¼ æ„Ÿå™¨è¿æ¥è‰¯å¥½ä¸”ä¸€åˆ‡æ­£å¸¸ï¼Œä½ åº”è¯¥èƒ½åœ¨ä¸²å£ç»ˆç«¯ä¸­çœ‹åˆ°ä¼ æ„Ÿå™¨çš„è§’åº¦å’Œé€Ÿåº¦è¾“å‡ºã€‚
 
 <blockquote class="info" markdown="1"> <p class="heading">â˜‘ï¸ ç®€å•æµ‹è¯•</p> 
 ç¡®ä¿æµ‹è¯•ç”µæœºæ—‹è½¬ä¸€å‘¨æ—¶ï¼Œè¾“å‡ºä¸º <b>6.28</b>ï¼ˆ2Ï€ï¼‰ `å¼§åº¦`ã€‚<br>å¦‚æœä½ ä¸ç†Ÿæ‚‰å¼§åº¦ï¼ˆRADï¼‰ï¼Œå¯ä»¥åœ¨[è¿™é‡Œ](library_units)æ‰¾åˆ°å…³äºåº“å•ä½çš„æ›´å¤šä¿¡æ¯ã€‚
 </blockquote>

## æ­¥éª¤ 2. ä»…æµ‹è¯•é©±åŠ¨å™¨
å½“ä½ çš„ä¼ æ„Ÿå™¨å·¥ä½œæ­£å¸¸åï¼Œå°±å¯ä»¥è¿›è¡Œé©±åŠ¨å™¨æµ‹è¯•äº†ã€‚æµ‹è¯•é©±åŠ¨å™¨æœ€ç®€å•çš„æ–¹æ³•æ˜¯ä½¿ç”¨åº“ç¤ºä¾‹ã€‚å¦‚æœæ—¶é—´å…è®¸ï¼Œä½ å¯ä»¥ä½¿ç”¨ `examples/utils/driver_standalone_test` æ–‡ä»¶å¤¹ä¸­çš„ç¤ºä¾‹æ¥æµ‹è¯•é©±åŠ¨å™¨ã€‚è¿™äº›ç¤ºä¾‹å°†é©±åŠ¨å™¨ä½œä¸ºç‹¬ç«‹æ¨¡å—è¿›è¡Œæµ‹è¯•ï¼Œé€šè¿‡å®ƒä»¬ä½ å¯ä»¥ä¸ºé©±åŠ¨å™¨çš„ä»»ä½•ç›¸è®¾ç½®ä»»æ„ç”µå‹å€¼ã€‚




<a href="javascript:show('3pwm','driver');" class="btn btn-driver btn-3pwm btn-primary">3PMW BLDC Driver</a> 
<a href ="javascript:show('6pwm','driver');"  class="btn btn-driver btn-6pwm">6PMW BLDC Driver</a> 
<a href ="javascript:show('2pwm','driver');"  class="btn btn-driver btn-2pwm">2PWM Stepper Driver</a>
<a href ="javascript:show('4pwm','driver');"  class="btn btn-driver btn-4pwm">4PWM Stepper Driver</a>


<div class="driver driver-3pwm"  markdown="1">

```cpp
#include <SimpleFOC.h>

// BLDCDriver3PWM(pwmA, pwmB, pwmC, (en optional))
BLDCDriver3PWM driver = BLDCDriver3PWM(9, 5, 6, 8);

void setup() {
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // Max DC voltage allowed - default voltage_power_supply
  driver.voltage_limit = 12;

  // driver init
  if (!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }

  // enable driver
  driver.enable();
  Serial.println("Driver ready!");
  _delay(1000);
}

void loop() {
    // setting pwm
    // phase A: 3V
    // phase B: 6V
    // phase C: 5V
    driver.setPwm(3,6,5);
}
```
</div>

<div class="driver driver-6pwm hide"  markdown="1">

```cpp
#include <SimpleFOC.h>

// BLDCDriver6PWM(pwmAh, pwmAl, pwmBh, pwmBl, pwmCh, pwmCl, (en optional))
BLDCDriver6PWM driver = BLDCDriver6PWM(5, 6, 9,10, 3, 11, 8);

void setup() {

  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // Max DC voltage allowed - default voltage_power_supply
  driver.voltage_limit = 12;
  // daad_zone [0,1] - default 0.02f - 2%
  driver.dead_zone = 0.05f;

  // driver init
  if (!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }

  // enable driver
  driver.enable();
  Serial.println("Driver ready!");
  _delay(1000);
}

void loop() {
    // setting pwm
    // phase A: 3V
    // phase B: 6V
    // phase C: 5V
    driver.setPwm(3,6,5);
}
```

</div>

<div class="driver driver-2pwm hide"  markdown="1">

```cpp
#include <SimpleFOC.h>

// StepperDriver2PWM(pwm1, dir1, pwm2, dir2,(en1, en2 optional))
StepperDriver2PWM driver = StepperDriver2PWM(3, 4, 5, 6, 11, 12);

void setup() {
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // Max DC voltage allowed - default voltage_power_supply
  driver.voltage_limit = 12;
  
  // driver init
  if (!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }

  // enable driver
  driver.enable();
  Serial.println("Driver ready!");
  _delay(1000);
}

void loop() {
    // setting pwm
    // phase A: 3V
    // phase B: 6V
    driver.setPwm(3,6);
}
```
</div>

<div class="driver driver-4pwm hide"  markdown="1">


```cpp
#include <SimpleFOC.h>

// StepperDriver4PWM(ph1A, ph1B, ph2A, ph2B, (en1, en2 optional))
StepperDriver4PWM driver = StepperDriver4PWM(5, 6, 9,10, 7, 8);

void setup() {
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // Max DC voltage allowed - default voltage_power_supply
  driver.voltage_limit = 12;
  
  // driver init
  if (!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }

  // enable driver
  driver.enable();
  Serial.println("Driver ready!");
  _delay(1000);
}

void loop() {
    // setting pwm
    // phase A: 3V
    // phase B: 6V
    driver.setPwm(3,6);
}
```
</div>

å‡†å¤‡å¥½ä»£ç åï¼Œå°†å…¶ä¸Šä¼ åˆ°ä½ çš„å¼€å‘æ¿å¹¶æ‰“å¼€ä¸²å£ç»ˆç«¯ã€‚å¦‚æœä¸€åˆ‡è¿æ¥æ­£ç¡®ï¼Œä½ çš„ PWM é©±åŠ¨å™¨åº”è¯¥å°±ç»ªï¼Œå¹¶ä¸”ä½ åº”è¯¥åœ¨ä¸²å£ç»ˆç«¯ä¸­çœ‹åˆ° `Driver ready!` æ¶ˆæ¯ã€‚å¦‚æœé©±åŠ¨å™¨åˆå§‹åŒ–ä¸æ­£ç¡®ï¼Œè¯·æ£€æŸ¥ä»¥ä¸‹äº‹é¡¹ï¼š
1. è¯·åŠ¡å¿…æ ¹æ®ä½ çš„åº”ç”¨ä¿®æ”¹é©±åŠ¨å™¨å‚æ•°ï¼Œä¾‹å¦‚å¼•è„šå·ã€PWM é¢‘ç‡ã€ç”µæºç”µå‹ç­‰ã€‚å¦‚æœä½ å¯¹æŸäº›å‚æ•°ä¸ç¡®å®šï¼Œè¯·æŸ¥é˜…  [é©±åŠ¨å™¨æ–‡æ¡£](drivers_config)ã€‚

2. ç¡®ä¿ä½¿ç”¨æ­£ç¡®çš„é©±åŠ¨å™¨ç±»å’Œæ­£ç¡®çš„ PWM å¼•è„šï¼Œè¿™é€šå¸¸å¾ˆç®€å•ï¼Œä½ å¯ä»¥åœ¨æˆ‘ä»¬çš„ [é©±åŠ¨å™¨æ–‡æ¡£](drivers_config) å’Œ [é€‰æ‹© PWM å¼•è„šæŒ‡å—](choosing_pwm_pins) ä¸­æ‰¾åˆ°å¾ˆå¤šç›¸å…³æ–‡æ¡£ã€‚å¦‚æœä½ ä¸ç¡®å®šä¸”æ— æ³•ä½¿å…¶å·¥ä½œï¼Œè¯·éšæ—¶åœ¨æˆ‘ä»¬çš„ [ç¤¾åŒºè®ºå›](https://community.simplefoc.com) ä¸Šæé—®ã€‚

<blockquote class="info" markdown="1"> <p class="heading">â˜‘ï¸ ç®€å•æµ‹è¯•</p>

1. æ‰“å¼€ä¸²å£ç›‘è§†å™¨ï¼Œç¡®è®¤å·²æ˜¾ç¤º `Driver ready!`ã€‚å¦‚æœæ˜¾ç¤º `Driver init failed!`ï¼Œè¯·æŸ¥çœ‹è°ƒè¯•è¾“å‡ºä»¥è·å–æ›´å¤šå…³äºå‡ºé”™åŸå› çš„ä¿¡æ¯ã€‚

2. ç¡®ä¿æ‰€æœ‰ç›¸éƒ½è¾“å‡ºPWMä¿¡å·ï¼Œä½ å¯ä»¥å°è¯•åœ¨æ¯ç›¸å’Œåœ°ä¹‹é—´è¿æ¥ä¸€ä¸ªå°LEDç¯ï¼Œæˆ–è€…ç”¨ä¸‡ç”¨è¡¨æµ‹é‡ã€‚
</blockquote>

## æ­¥éª¤ 3. æµ‹è¯•é©±åŠ¨å™¨ + ç”µæœºç»„åˆ - å¼€ç¯æ§åˆ¶
å¦‚æœä½ å·²ç»è¿æ¥å¥½ç”µæœºï¼Œå¹¶ä¸”ç¡®å®šé©±åŠ¨å™¨å·¥ä½œæ­£å¸¸ï¼Œæˆ‘ä»¬å»ºè®®ä½ ä½¿ç”¨ `examples/motion_control/open_loop_motion_control `ä¸­çš„å¼€ç¯æ§åˆ¶ç¤ºä¾‹æ¥æµ‹è¯•ç”µæœº + é©±åŠ¨å™¨ç»„åˆã€‚å¦‚æœä½ çš„é©±åŠ¨å™¨ä¸ç¤ºä¾‹ä¸­æä¾›çš„ä¸åŒï¼Œè¯·æŸ¥é˜… [é©±åŠ¨å™¨æ–‡æ¡£](drivers_config)ï¼Œæ‰¾åˆ°é€‚åˆä½ çš„é©±åŠ¨å™¨å’Œä»£ç ã€‚æ­¤å¤–ï¼Œä½ è¿˜å¯ä»¥æµè§ˆ` examples/utils/driver_standalone_test `æ–‡ä»¶å¤¹ä¸­çš„ç¤ºä¾‹ï¼ŒæŸ¥çœ‹å…¶ä¸­çš„ç”¨æ³•ã€‚

<blockquote class="warning"> <p class="heading">è¯·æ³¨æ„</p>
åœ¨å°è¯•æ­¤ç¤ºä¾‹ä¹‹å‰ï¼Œè¯·è‡³å°‘ç®€è¦æŸ¥çœ‹ä»£ç ä¸‹æ–¹çš„è§„åˆ™ã€‚ä»è¿™é‡Œå¼€å§‹ï¼Œå®é™…ç”µæµå°†æµè¿‡ä½ çš„ç”µå­è®¾å¤‡ï¼Œå¹¶ä¸”ä½ çš„é©±åŠ¨å™¨æ— æ³•æµ‹é‡å®ƒï¼ˆè‡³å°‘åœ¨æ­¤ç¤ºä¾‹ä¸­ä¸èƒ½ï¼‰ã€‚</blockquote>

å› æ­¤ï¼Œæ­¤æ­¥éª¤çš„ä¸»è¦ç›®çš„æ˜¯æµ‹è¯•ç”µæœºå‚æ•°ï¼Œä¾‹å¦‚æå¯¹æ•°ï¼Œå¹¶éªŒè¯é©±åŠ¨å™¨æ˜¯å¦èƒ½å¤Ÿé©±åŠ¨ç”µæœºã€‚

<a href="javascript:show('bldc3','open');" class="btn btn-open btn-bldc3 btn-primary">BLDC Motor + 3PMW driver</a>
<a href="javascript:show('bldc6','open');" class="btn btn-open btn-bldc6">BLDC Motor + 6PMW driver</a> 
<a href ="javascript:show('stepper2','open');"  class="btn btn-open btn-stepper2">Stepper Motor + 2PMW driver</a> 
<a href ="javascript:show('stepper4','open');"  class="btn btn-open btn-stepper4">Stepper Motor + 4PMW driver</a> 

<div class="open open-bldc3" markdown="1">

ä¸‹é¢æ˜¯ä½¿ç”¨**BLDC motor** å’Œ **3PWM driver**çš„å¼€ç¯é€Ÿåº¦æ§åˆ¶ç¤ºä¾‹ã€‚.

```cpp
#include <SimpleFOC.h>

// BLDCMotor(pole pair number, phase resistance (optional) );
BLDCMotor motor = BLDCMotor(11);
// BLDCDriver3PWM(pwmA, pwmB, pwmC, Enable(optional));
BLDCDriver3PWM driver = BLDCDriver3PWM(9, 5, 6, 8);

// instantiate the commander
Commander command = Commander(Serial);
void doTarget(char* cmd) { command.scalar(&motor.target, cmd); }
void doLimit(char* cmd) { command.scalar(&motor.voltage_limit, cmd); }

void setup() {

  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // limit the maximal dc voltage the driver can set
  // as a protection measure for the low-resistance motors
  // this value is fixed on startup
  driver.voltage_limit = 6;
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link the motor and the driver
  motor.linkDriver(&driver);

  // limiting motor movements
  // limit the voltage to be set to the motor
  // start very low for high resistance motors
  // current = voltage / resistance, so try to be well under 1Amp
  motor.voltage_limit = 3;   // [V]
 
  // open loop control config
  motor.controller = MotionControlType::velocity_openloop;

  // init motor hardware
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }

  // set the target velocity [rad/s]
  motor.target = 6.28; // one rotation per second

  // add target command T
  command.add('T', doTarget, "target velocity");
  command.add('L', doLimit, "voltage limit");

  Serial.println("Motor ready!");
  Serial.println("Set target velocity [rad/s]");
  _delay(1000);
}

void loop() {

  // open loop velocity movement
  motor.move();

  // user communication
  command.run();
}
```

</div>

<div class="open open-bldc6 hide" markdown="1">


ä¸‹é¢æ˜¯ä½¿ç”¨  **BLDC motor** å’Œ **6PWM driver** çš„å¼€ç¯é€Ÿåº¦æ§åˆ¶ç¤ºä¾‹ã€‚

```cpp
#include <SimpleFOC.h>

// BLDCMotor(pole pair number, phase resistance (optional) );
BLDCMotor motor = BLDCMotor(11);
// BLDCDriver6PWM(pwmAh, pwmAl, pwmBh, pwmBl, pwmCh, pwmCl, (en optional))
BLDCDriver6PWM driver = BLDCDriver6PWM(5, 6, 9,10, 3, 11, 8);

// instantiate the commander
Commander command = Commander(Serial);
void doTarget(char* cmd) { command.scalar(&motor.target, cmd); }
void doLimit(char* cmd) { command.scalar(&motor.voltage_limit, cmd); }

void setup() {

  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // limit the maximal dc voltage the driver can set
  // as a protection measure for the low-resistance motors
  // this value is fixed on startup
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link the motor and the driver
  motor.linkDriver(&driver);

  // limiting motor movements
  // limit the voltage to be set to the motor
  // start very low for high resistance motors
  // current = voltage / resistance, so try to be well under 1Amp
  motor.voltage_limit = 3;   // [V]
 
  // open loop control config
  motor.controller = MotionControlType::velocity_openloop;

  // init motor hardware
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }

  // set the target velocity [rad/s]
  motor.target = 6.28; // one rotation per second

  // add target command T
  command.add('T', doTarget, "target velocity");
  command.add('L', doLimit, "voltage limit");

  Serial.println("Motor ready!");
  Serial.println("Set target velocity [rad/s]");
  _delay(1000);
}

void loop() {

  // open loop velocity movement
  motor.move();

  // user communication
  command.run();
}
```

</div>

<div class="open open-stepper2 hide"  markdown="1">


ä¸‹é¢æ˜¯ä½¿ç”¨ **Stepper motor** å’Œ **2PWM driver**çš„å¼€ç¯é€Ÿåº¦æ§åˆ¶ç¤ºä¾‹ã€‚

```cpp
#include <SimpleFOC.h>

// StepperMotor(pole pair number, phase resistance (optional) );
StepperMotor motor = StepperMotor(50);
// StepperDriver2PWM(pwm1, dir1, pwm2, dir2,(en1, en2 optional))
StepperDriver2PWM driver = StepperDriver2PWM(9, 5, 6, 8);

// instantiate the commander
Commander command = Commander(Serial);
void doTarget(char* cmd) { command.scalar(&motor.target, cmd); }
void doLimit(char* cmd) { command.scalar(&motor.voltage_limit, cmd); }

void setup() {

  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // limit the maximal dc voltage the driver can set
  // as a protection measure for the low-resistance motors
  // this value is fixed on startup
  driver.voltage_limit = 6;
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link the motor and the driver
  motor.linkDriver(&driver);

  // limiting motor movements
  // limit the voltage to be set to the motor
  // start very low for high resistance motors
  // current = voltage / resistance, so try to be well under 1Amp
  motor.voltage_limit = 3;   // [V]
 
  // open loop control config
  motor.controller = MotionControlType::velocity_openloop;

  // init motor hardware
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }

  // set the target velocity [rad/s]
  motor.target = 6.28; // one rotation per second

  // add target command T
  command.add('T', doTarget, "target velocity");
  command.add('L', doLimit, "voltage limit");

  Serial.println("Motor ready!");
  Serial.println("Set target velocity [rad/s]");
  _delay(1000);
}

void loop() {

  // open loop velocity movement
  motor.move();

  // user communication
  command.run();
}
```
</div>

<div class="open open-stepper4 hide"  markdown="1">


ä¸‹é¢æ˜¯ä½¿ç”¨ **4PWM driver** å’Œ **Stepper motor**çš„å¼€ç¯é€Ÿåº¦æ§åˆ¶ç¤ºä¾‹ã€‚
```cpp
#include <SimpleFOC.h>

// StepperMotor(pole pair number, phase resistance (optional) );
StepperMotor motor = StepperMotor(50);
// StepperDriver4PWM(ph1A, ph1B, ph2A, ph2B, (en1, en2 optional))
StepperDriver4PWM driver = StepperDriver4PWM(5, 6, 9,10, 7, 8);

// instantiate the commander
Commander command = Commander(Serial);
void doTarget(char* cmd) { command.scalar(&motor.target, cmd); }
void doLimit(char* cmd) { command.scalar(&motor.voltage_limit, cmd); }

void setup() {

  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // limit the maximal dc voltage the driver can set
  // as a protection measure for the low-resistance motors
  // this value is fixed on startup
  driver.voltage_limit = 6;
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link the motor and the driver
  motor.linkDriver(&driver);

  // limiting motor movements
  // limit the voltage to be set to the motor
  // start very low for high resistance motors
  // current = voltage / resistance, so try to be well under 1Amp
  motor.voltage_limit = 3;   // [V]
 
  // open loop control config
  motor.controller = MotionControlType::velocity_openloop;

  // init motor hardware
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }

  // set the target velocity [rad/s]
  motor.target = 6.28; // one rotation per second

  // add target command T
  command.add('T', doTarget, "target velocity");
  command.add('L', doLimit, "voltage limit");

  Serial.println("Motor ready!");
  Serial.println("Set target velocity [rad/s]");
  _delay(1000);
}

void loop() {

  // open loop velocity movement
  motor.move();

  // user communication
  command.run();
}
```
</div>

å‡†å¤‡å¥½ä»£ç åï¼Œå°†å…¶ä¸Šä¼ åˆ°ä½ çš„å¼€å‘æ¿å¹¶æ‰“å¼€ä¸²å£ç»ˆç«¯ã€‚å¦‚æœä¸€åˆ‡è¿æ¥æ­£ç¡®ï¼Œä½ åº”è¯¥çœ‹åˆ°ç”µæœºæ—‹è½¬ã€‚å¦‚æœç”µæœºä¸æ—‹è½¬ï¼Œè¯·æ£€æŸ¥ä»¥ä¸‹äº‹é¡¹ï¼š
1. ç¡®ä¿ä½¿ç”¨æ­£ç¡®çš„é©±åŠ¨å™¨ç±»å’Œæ­£ç¡®çš„ PWM å¼•è„šï¼Œè¿™é€šå¸¸å¾ˆç®€å•ï¼Œä½ å¯ä»¥åœ¨æˆ‘ä»¬çš„ [é©±åŠ¨å™¨æ–‡æ¡£](drivers_config) å’Œ [é€‰æ‹© PWM å¼•è„šæŒ‡å—](choosing_pwm_pins) ä¸­æ‰¾åˆ°å¾ˆå¤šç›¸å…³æ–‡æ¡£ã€‚

2. ç¡®ä¿ä½¿ç”¨é€‚å½“çš„ç”µå‹é™åˆ¶ã€‚æ‰€æœ‰è¿™äº›ç¤ºä¾‹éƒ½å‡è®¾ä½ ä½¿ç”¨çš„æ˜¯é«˜ç›¸ç”µé˜»ï¼ˆ~10 æ¬§å§†ï¼‰çš„äº‘å°å‹ç”µæœºã€‚å¯¹äºæ›´é«˜åŠŸç‡çš„ç”µæœºï¼Œç”µæµå¯èƒ½ä¼šè¿‡é«˜ï¼Œä½¿ä½ çš„ç”µæœº + é©±åŠ¨å™¨å¤„äºå±é™©ä¹‹ä¸­ã€‚æ ¹æ®ç»éªŒï¼Œå¦‚æœä½ çš„ç”µæœºæœ‰ä¼ºæœå‹æ’å¤´ï¼Œåº”è¯¥æ²¡é—®é¢˜ã€‚
`motor.voltage_limit` å°†ç›´æ¥å†³å®šé€šè¿‡ç”µæœºçš„ç”µæµï¼šç”µæµ = `motor.voltage_limit / motor`.`phase_resistance`ã€‚
å› æ­¤ï¼Œä¸ºé¿å…è¿‡é«˜çš„ç”µæµï¼Œè¯·å°è¯•æ‰¾å‡ºç”µæœºçš„ç›¸ç”µé˜»ï¼Œå¹¶è®¾ç½® `motor.voltage_limit` ä½¿å¾—ç”µæµä¸è¶…è¿‡ 2Aï¼Œä¾‹å¦‚ï¼š`motor.voltage_limit = 2 ` ç›¸ç”µé˜»ã€‚æœ€å¥½çš„æ–¹æ³•æ˜¯é€šè¿‡è®¾ç½®å‚æ•° `motor.phase_resistance` å‘ç”µæœºæä¾›ç›¸ç”µé˜»å€¼ï¼Œç„¶åä½ å¯ä»¥ä½¿ç”¨ `motor.current_limit` è€Œä¸æ˜¯ `motor.voltage_limit`ï¼Œè¿™ä¼šä½¿é—®é¢˜ç®€å•å¾—å¤šã€‚å¦‚æœä½ æƒ³å°è¯•æµ‹é‡ç”µæœºçš„ç›¸ç”µé˜»ï¼Œå¯ä»¥åœ¨ [è¿™é‡Œ](phase_resistance) æ‰¾åˆ°æˆ‘ä»¬çš„æŒ‡å—ã€‚
å¦‚æœä½ æ‰¾ä¸åˆ°ç”µæœºçš„ç›¸ç”µé˜»å¹¶ä¸”æ— æ³•ç”¨ä¸‡ç”¨è¡¨æµ‹é‡ï¼Œè¯·ä»å°å¤„å¼€å§‹ï¼šmotor.voltage_limit < 1;

3. ç¡®ä¿è¾“å…¥æ­£ç¡®çš„æå¯¹æ•°ã€‚ä½ å¯ä»¥åœ¨å¤§å¤šæ•°æ•°æ®æ‰‹å†Œä¸­æ‰¾åˆ°ï¼Œå¦‚æœä½ ä¸ç¡®å®šå®é™…æ•°é‡ï¼Œåˆ«æ‹…å¿ƒï¼Œè¿™ä¸€æ­¥æ—¨åœ¨æµ‹è¯• / æ‰¾åˆ°ç¡®åˆ‡çš„å€¼ã€‚ğŸ˜„

<blockquote class="info" markdown="1"> <p class="heading">â˜‘ï¸ ç®€å•æµ‹è¯•</p>

1. åœ¨ä¸²å£ç»ˆç«¯ä¸­ç¡®è®¤ç”µæœºå’Œé©±åŠ¨å™¨å·²æˆåŠŸåˆå§‹åŒ–ã€‚ç¡®ä¿ä½ èƒ½çœ‹åˆ° `ç”µæœºå°±ç»ªï¼`ï¼Œè€Œä¸æ˜¯ `ç”µæœºåˆå§‹åŒ–å¤±è´¥ï¼` æˆ– `é©±åŠ¨å™¨åˆå§‹åŒ–å¤±è´¥ï¼`ã€‚
2. åœ¨é€Ÿåº¦æ¨¡å¼ä¸‹ï¼š(`motor.controller = MotionControlType::velocity_openloop;`) <br>
å°†ç›®æ ‡é€Ÿåº¦è®¾ç½®ä¸ºï¼ˆ`T6.28`ï¼‰<b>6.28ï¼ˆ$2Ï€$ï¼‰å¼§åº¦/ç§’</b>ï¼ˆè¾“å…¥ `T6.28`ï¼‰ï¼Œè¿™åº”è¯¥æ­£å¥½æ˜¯æ¯ç§’ä¸€åœˆã€‚
3. åœ¨ä½ç½®æ¨¡å¼ä¸‹ï¼Œ(`motor.controller = MotionControlType::angle_openloop;`) <br>
å°†ç›®æ ‡ä½ç½®è®¾ç½®ä¸º <b>6.28ï¼ˆ$2Ï€$ï¼‰å¼§åº¦</b>ï¼ˆè¾“å…¥ `T6.28`ï¼‰ï¼Œå®ƒåº”è¯¥æ­£å¥½æ˜¯ä¸€åœˆã€‚

å¦‚æœä½ çš„ç”µæœºåœ¨é€Ÿåº¦æ¨¡å¼ä¸‹æ¯ç§’æ—‹è½¬çš„åœˆæ•°ä¸æ˜¯ä¸€åœˆï¼Œæˆ–è€…åœ¨è§’åº¦æ¨¡å¼ä¸‹æ—‹è½¬çš„åœˆæ•°ä¸æ˜¯ä¸€åœˆï¼Œè¿™å¯èƒ½æ„å‘³ç€ä½ çš„æå¯¹æ•°ä¸æ­£ç¡®ã€‚å°è¯•æ›´æ”¹å®ƒï¼Œç›´åˆ°è·å¾—æ­£å¥½ä¸€åœˆï¼ˆæˆ–åœ¨é€Ÿåº¦æ¨¡å¼ä¸‹æ¯ç§’ä¸€åœˆï¼‰ã€‚
 
 </blockquote>

## æ­¥éª¤ 4. é—­ç¯æ§åˆ¶ - ä½¿ç”¨ç”µå‹çš„ torque æ§åˆ¶

ä¸€æ—¦ä½ æœ‰äº†å·¥ä½œæ­£å¸¸çš„ä¼ æ„Ÿå™¨ã€ç”µæœºå’Œé©±åŠ¨å™¨ï¼Œå°±å¯ä»¥è¿›è¡Œé—­ç¯æ§åˆ¶æµ‹è¯•äº†ã€‚é¦–å…ˆè¦æµ‹è¯•çš„æ˜¯ä½¿ç”¨ç”µå‹çš„ torque æ§åˆ¶æ¨¡å¼ï¼Œè¿™æ˜¯ SimpleFOClibrary ä¸­å¯ç”¨çš„æœ€ç®€å•çš„é—­ç¯æ§åˆ¶å½¢å¼ã€‚ä½ å¯ä»¥åœ¨åº“ç¤ºä¾‹æ–‡ä»¶å¤¹` examples/motion_control/torque_control` ä¸­æ‰¾åˆ°ä¸åŒä¼ æ„Ÿå™¨çš„è¿™ç§ torque æ¨¡å¼ç¤ºä¾‹ã€‚

<a href="javascript:show('temp','cl');" class="btn btn-cl btn-temp btn-primary">Sketch template</a>
<a href="javascript:show('bldc','cl');" class="btn btn-cl btn-bldc">BLDC Motor + 3PMW driver + Encoder</a> 
<a href ="javascript:show('stepper','cl');"  class="btn btn-cl btn-stepper">Stepper Motor + 2PMW driver + Encoder</a> 



<div class="cl cl-temp" markdown="1">


ä¸‹é¢æ˜¯ä¸€ä¸ªå¸¦æœ‰` TODO `æ¡ç›®çš„ç¤ºä¾‹è‰å›¾ï¼Œä½ å¯ä»¥ä¸ºä½ çš„ç”µæœºã€é©±åŠ¨å™¨å’Œä¼ æ„Ÿå™¨å®ç°è¿™äº›æ¡ç›®ã€‚

```cpp
#include <SimpleFOC.h>

// TODO: motor instance 
// TODO: driver instance

// TODO: sensor instance

// instantiate the commander
Commander command = Commander(Serial);
void doMotor(char* cmd) { command.motor(&motor, cmd); }

void setup() { 
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);

  // TODO: driver config
  // init the sensor
  sensor.init()
  // link the motor to the sensor
  motor.linkSensor(&sensor);

  // TODO: driver config
  // init the driver
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link driver
  motor.linkDriver(&driver);

  // TODO: motor conf

  // set motion control loop to be used
  motor.torque_controller = TorqueControlType::voltage;
  motor.controller = MotionControlType::torque;

  // use monitoring with serial
  // comment out if not needed
  motor.useMonitoring(Serial);

  // initialize motor
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }
  // align sensor and start FOC
  if(!motor.initFOC()){
    Serial.println("FOC init failed!");
    return;
  }

  // set the initial motor target
  motor.target = 2; // Volts 

  // add target command M
  command.add('M', doMotor, "Motor");

  Serial.println(F("Motor ready."));
  Serial.println(F("Set the target using serial terminal and command M:"));
  _delay(1000);
}

void loop() {
  // main FOC algorithm function
  motor.loopFOC();

  // Motion control function
  motor.move();

  // user communication
  command.run();
}
```
</div>

<div class="cl cl-bldc hide" markdown="1">

ä¸‹é¢æ˜¯ä½¿ç”¨ **3PWM driver** å’Œ **Encoder**ä¼ æ„Ÿå™¨çš„ **BLDC motor** çš„ç¤ºä¾‹è‰å›¾ã€‚

```cpp
#include <SimpleFOC.h>

// BLDC motor & driver instance
BLDCMotor motor = BLDCMotor(11);
BLDCDriver3PWM driver = BLDCDriver3PWM(9, 5, 6, 8);

// encoder instance
Encoder encoder = Encoder(2, 3, 500);
// channel A and B callbacks
void doA(){encoder.handleA();}
void doB(){encoder.handleB();}

// instantiate the commander
Commander command = Commander(Serial);
void doMotor(char* cmd) { command.motor(&motor, cmd); }

void setup() { 
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // initialize encoder sensor hardware
  encoder.init();
  encoder.enableInterrupts(doA, doB); 
  // link the motor to the sensor
  motor.linkSensor(&encoder);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // driver init
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link driver
  motor.linkDriver(&driver);

  // aligning voltage
  motor.voltage_sensor_align = 5;

  // set motion control loop to be used
  motor.torque_controller = TorqueControlType::voltage;
  motor.controller = MotionControlType::torque;

  // comment out if not needed
  motor.useMonitoring(Serial);

  // initialize motor
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }
  // align sensor and start FOC
  if(!motor.initFOC()){
    Serial.println("FOC init failed!");
    return;
  }

  // set the initial motor target
  motor.target = 2; // Volts 

  // add target command M
  command.add('M', doMotor, "Motor");

  Serial.println(F("Motor ready."));
  Serial.println(F("Set the target using serial terminal and command M:"));
  _delay(1000);
}

void loop() {
  // main FOC algorithm function
  motor.loopFOC();

  // Motion control function
  motor.move();

  // user communication
  command.run();
}
```
</div>

<div class="cl cl-stepper hide" markdown="1">

ä¸‹é¢æ˜¯ä½¿ç”¨ **2PWM driver** å’Œ **Encoder** ä¼ æ„Ÿå™¨çš„ **Stepper motor** çš„ç¤ºä¾‹è‰å›¾ã€‚

```cpp
#include <SimpleFOC.h>

// Stepper motor & driver instance
StepperMotor motor = StepperMotor(11);
StepperDriver2PWM driver = StepperDriver2PWM(9, 5, 6, 8);

// encoder instance
Encoder encoder = Encoder(2, 3, 500);
// channel A and B callbacks
void doA(){encoder.handleA();}
void doB(){encoder.handleB();}

// instantiate the commander
Commander command = Commander(Serial);
void doMotor(char* cmd) { command.motor(&motor, cmd); }

void setup() { 
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // initialize encoder sensor hardware
  encoder.init();
  encoder.enableInterrupts(doA, doB); 
  // link the motor to the sensor
  motor.linkSensor(&encoder);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // driver init
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link driver
  motor.linkDriver(&driver);

  // aligning voltage
  motor.voltage_sensor_align = 5;

  // set motion control loop to be used
  motor.torque_controller = TorqueControlType::voltage;
  motor.controller = MotionControlType::torque;

  // comment out if not needed
  motor.useMonitoring(Serial);

  // initialize motor
  motor.init();
  // align sensor and start FOC
  if(!motor.initFOC()){
    Serial.println("FOC init failed!");
    return;
  }

  // set the initial motor target
  motor.target = 2; // Volts 

  // add target command M
  command.add('M', doMotor, "Motor");

  Serial.println(F("Motor ready."));
  Serial.println(F("Set the target using serial terminal and command M:"));
  _delay(1000);
}

void loop() {
  // main FOC algorithm function
  motor.loopFOC();

  // Motion control function
  motor.move();

  // user communication
  command.run();
}
```
</div>

åˆ°ç›®å‰ä¸ºæ­¢ï¼Œä½ å·²ç»çŸ¥é“äº†ä¼ æ„Ÿå™¨ã€é©±åŠ¨å™¨å’Œç”µæœºçš„è‰¯å¥½é…ç½®ã€‚å¦‚æœåˆ°ç›®å‰ä¸ºæ­¢ä¸€åˆ‡æ­£å¸¸ï¼Œä½ å¾ˆå¯èƒ½åœ¨è¿™ä¸€æ­¥ä¹Ÿä¸ä¼šé‡åˆ°ä»»ä½•é—®é¢˜ã€‚ä½ å¯ä»¥æ ¹æ®è‡ªå·±çš„è®¾ç½®è°ƒæ•´ä¸Šè¿°ç¤ºä¾‹ï¼Œå¹¶å°†ä»£ç ä¸Šä¼ åˆ°å¼€å‘æ¿ã€‚

<blockquote class="info" markdown="1"> <p class="heading">â˜‘ï¸ ç®€å•æµ‹è¯•</p>

1.  å¦‚æœä¸€åˆ‡è¿æ¥æ­£ç¡®ï¼Œä½ åº”è¯¥çœ‹åˆ°ç”µæœºæ—‹è½¬ï¼Œå¹¶ä¸”èƒ½å¤Ÿé€šè¿‡ä¸²å£ç»ˆç«¯ï¼ˆå‘½ä»¤ `M`ï¼‰è®¾ç½®ç›®æ ‡ç”µå‹ã€‚
2.  å¦‚æœç”µæœºä¸æ—‹è½¬ï¼Œè¯·é€šè¿‡æ£€æŸ¥ä¸²å£ç»ˆç«¯è¾“å‡ºæ¥ç¡®è®¤ç”µæœºå’Œé©±åŠ¨å™¨å·²æˆåŠŸåˆå§‹åŒ–ï¼ˆç¡®ä¿ä½ èƒ½çœ‹åˆ° `ç”µæœºå°±ç»ªï¼`ï¼Œè€Œä¸æ˜¯ `ç”µæœºåˆå§‹åŒ–å¤±è´¥ï¼` æˆ– `é©±åŠ¨å™¨åˆå§‹åŒ–å¤±è´¥ï¼`ï¼‰ã€‚
</blockquote>

å¯èƒ½å‡ºç°çš„æœ€å¸¸è§é—®é¢˜æ˜¯ï¼š
- ç”µæœºæ ¹æœ¬ä¸æ—‹è½¬
   - é©±åŠ¨å™¨å¯èƒ½æ²¡æœ‰æ­£ç¡®åˆå§‹åŒ–ï¼Œè¯·æ£€æŸ¥ä¸²å£ç»ˆç«¯è¾“å‡ºæ˜¯å¦æœ‰ä»»ä½•é”™è¯¯ã€‚
- ç”µæœºåœ¨ `motor.initFOC()` å‡½æ•°æ‰§è¡ŒæœŸé—´æ—‹è½¬ï¼Œä½†ä¹‹åä¸æ—‹è½¬
   - éªŒè¯ `motor.initFOC()` å‡½æ•°çš„è¾“å‡ºï¼Œå¯èƒ½æ˜¯ä¼ æ„Ÿå™¨å¯¹é½æˆ–æå¯¹æ•°æœ‰é—®é¢˜ã€‚
- ç”µæœºæ—‹è½¬ä½†ä¸ç¨³å®šä¸”å™ªéŸ³å¾ˆå¤§
   - éªŒè¯ `motor.initFOC()` å‡½æ•°çš„è¾“å‡º
      - æ£€æŸ¥æå¯¹æ•°æ£€æŸ¥ï¼Œå¦‚æœå¤±è´¥ï¼Œå¯èƒ½æ„å‘³ç€æå¯¹æ•°ä¸æ­£ç¡®ï¼Œè¯·å°è¯•æ›´æ”¹ã€‚
      - å¦‚æœä¸²å£ç»ˆç«¯ä¸­ä¸€åˆ‡ä¼¼ä¹æ­£å¸¸ï¼Œä¼ æ„Ÿå™¨å¯èƒ½æ²¡æœ‰æ­£ç¡®å¯¹é½ï¼Œè¯·å°è¯•å‡é«˜ / é™ä½ `motor.voltage_sensor_align` å€¼ã€‚

<blockquote class="warning" markdown="1"> <p class="heading">è¯·æ³¨æ„</p>

ç¡®ä¿ä½¿ç”¨ä¸å¤ªé«˜çš„ `motor.voltage_sensor_align` å€¼ï¼Œä»¥é˜²æ­¢è¿‡é«˜çš„ç”µæµã€‚è§„åˆ™ä¸å¼€ç¯æ§åˆ¶ä¸­çš„ `motor.voltage_limit` ç›¸åŒã€‚å¦‚æœä½ ä¸ç¡®å®šä½ çš„ç›¸ç”µé˜»ï¼Œè¯·ä»å°å¤„å¼€å§‹ï¼š`motor.voltage_sensor_align < 1`ã€‚æ­¤å¤–ï¼Œä½ ä¹Ÿå¯ä»¥å®šä¹‰ç”µæœºç›¸ç”µé˜»ï¼Œç„¶åä½¿ç”¨ `motor.current_limit` å˜é‡ï¼Œè¯¥å˜é‡å°†é™åˆ¶ `motor.voltage_sensor_align`ï¼Œä½ å°±ä¸å¿…å†æ‹…å¿ƒå®ƒäº†ã€‚ä½†æ˜¯ï¼Œå¦‚æœä½ æŒ‡å®šäº†ç›¸ç”µé˜»å€¼ï¼Œä½ å°†ä¸å†å‘ç”µæœºè®¾ç½®ç”µå‹å‘½ä»¤ï¼Œè€Œæ˜¯ç”µæµå‘½ä»¤ï¼Œæœ‰å…³æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚è§ [torque æ§åˆ¶æ–‡æ¡£](voltage_torque_mode)ã€‚</blockquote>

### æ­¥éª¤ 4.1. é€Ÿåº¦å’Œè§’åº¦æ§åˆ¶

ä¸€æ—¦ä¸Šè¿°ä»£ç è¿è¡Œè‰¯å¥½ï¼Œä½ å°±å¯ä»¥è¿›è¡Œé€Ÿåº¦å’Œè§’åº¦è¿åŠ¨æ§åˆ¶äº†ã€‚
- è¦ä» torque æ§åˆ¶åˆ‡æ¢åˆ°é€Ÿåº¦æ§åˆ¶ï¼Œåªéœ€å°† `motor.controller` å˜é‡æ›´æ”¹ä¸º `MotionControlType::velocity`ï¼Œå¹¶å°† `motor.target` è®¾ç½®ä¸ºæ‰€éœ€é€Ÿåº¦ï¼ˆä»¥å¼§åº¦ / ç§’ RAD/s ä¸ºå•ä½ï¼‰ã€‚
- è¦ä» torque æ§åˆ¶åˆ‡æ¢åˆ°è§’åº¦æ§åˆ¶ï¼Œåªéœ€å°† `motor.controller` å˜é‡æ›´æ”¹ä¸º `MotionControlType::angle`ï¼Œå¹¶å°† `motor.target` è®¾ç½®ä¸ºæ‰€éœ€è§’åº¦ï¼ˆä»¥å¼§åº¦ RAD ä¸ºå•ä½ï¼‰ã€‚
- é€šè¿‡ `Commander` æ¥å£é€šè¿‡ä¸²å£ç»ˆç«¯åˆ‡æ¢åˆ°é€Ÿåº¦æ§åˆ¶çš„å¦ä¸€ç§æ–¹æ³•
  - è¦å¯åŠ¨é€Ÿåº¦æ§åˆ¶æ¨¡å¼ï¼Œè¯·åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `MC1` å¹¶æŒ‰å›è½¦ã€‚
    - è¦è®¾ç½®ç›®æ ‡é€Ÿåº¦ï¼Œè¯·åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `M6.28` å¹¶æŒ‰å›è½¦ã€‚è¿™å°†æŠŠç›®æ ‡é€Ÿåº¦è®¾ç½®ä¸ºæ¯ç§’ä¸€åœˆã€‚
    - è¦åœæ­¢ç”µæœºï¼Œè¯·åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `M0` å¹¶æŒ‰å›è½¦ã€‚è¿™å°†æŠŠç›®æ ‡é€Ÿåº¦è®¾ç½®ä¸ºé›¶ã€‚
  - è¦å¯åŠ¨è§’åº¦æ§åˆ¶æ¨¡å¼ï¼Œè¯·åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `MC2` å¹¶æŒ‰å›è½¦ã€‚
    - è¦è®¾ç½®ç›®æ ‡è§’åº¦ï¼Œè¯·åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `M0` å¹¶æŒ‰å›è½¦ã€‚è¿™å°†æŠŠç›®æ ‡è§’åº¦è®¾ç½®ä¸ºä¸€åœˆã€‚
    - å½“ä½ çš„ç”µæœºå¤„äº `0` è§’åº¦æ—¶ï¼Œå°è¯•åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `M6.28` å¹¶æŒ‰å›è½¦ã€‚è¿™å°†æŠŠç›®æ ‡è§’åº¦è®¾ç½®ä¸ºä¸€åœˆã€‚
  - è¦è¿”å› torque æ§åˆ¶æ¨¡å¼ï¼Œè¯·åœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `MC0 `å¹¶æŒ‰å›è½¦ã€‚
  - æœ‰å…³` Commander `æ¥å£çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚è§ [Commander æ–‡æ¡£ã€‚](commander_interface)


é€Ÿåº¦å’Œè§’åº¦è¿åŠ¨æ§åˆ¶æ¨¡å¼æœ‰é¢å¤–çš„é…ç½®å‚æ•°ï¼Œå¯èƒ½éœ€è¦è¿™äº›å‚æ•°æ¥è·å¾—ç”µæœºçš„å¹³ç¨³å’Œç¨³å®šè¿è¡Œã€‚ä½ å¯ä»¥åœ¨ [è¿åŠ¨æ§åˆ¶æ–‡æ¡£](closed_loop_motion_control) ä¸­æ‰¾åˆ°æœ‰å…³è¿™äº›å‚æ•°çš„æ›´å¤šä¿¡æ¯ã€‚

## æ­¥éª¤ 5. æµ‹è¯•ç”µæµæ£€æµ‹ï¼ˆå¦‚æœå¯ç”¨ï¼‰
å¦‚æœä½ çš„è®¾ç½®å…·æœ‰ SimpleFOClibrary æ”¯æŒçš„ç”µæµæ£€æµ‹åŠŸèƒ½ï¼Œé‚£ä¹ˆæˆ‘ä»¬å»ºè®®ä½ åœ¨æ‰§è¡Œæ­¤æ­¥éª¤ä¹‹å‰ï¼Œè‡³å°‘ç¡®ä¿èƒ½å¤Ÿä½¿ç”¨ç”µå‹è¿›è¡Œé—­ç¯ torque æ§åˆ¶ï¼ˆæ­¥éª¤ 4ï¼‰ã€‚

æœ€å¥½çš„å¼€å§‹æ–¹æ³•æ˜¯å°†ç”µæµæ£€æµ‹æ·»åŠ åˆ°ä½¿ç”¨ç”µå‹çš„ torque æ§åˆ¶ä»£ç ï¼ˆæ­¥éª¤ 4ï¼‰ä¸­ï¼Œå¹¶ä½¿ç”¨ç›‘æ§å°† d å’Œ q ç”µæµè¾“å‡ºåˆ°ä¸²å£ç»ˆç«¯ã€‚

<a href="javascript:show('temp','current');" class="btn btn-current btn-temp btn-primary">Sketch template</a>
<a href="javascript:show('bldc','current');" class="btn btn-current btn-bldc">BLDC Motor + 3PMW driver + Encoder + Inline Current Sensing</a> 
<a href ="javascript:show('stepper','current');"  class="btn btn-current btn-stepper">Stepper Motor + 2PMW driver + Encoder + Inline Current Sensing</a> 



<div class="current current-temp" markdown="1">


ä¸‹é¢æ˜¯ä¸€ä¸ªå¸¦æœ‰ `TODO` æ¡ç›®çš„ç¤ºä¾‹è‰å›¾ï¼Œä½ å¯ä»¥ä¸ºä½ çš„ç”µæœºã€é©±åŠ¨å™¨ã€ç”µæµæ£€æµ‹å’Œä¼ æ„Ÿå™¨å®ç°è¿™äº›æ¡ç›®ã€‚

```cpp
#include <SimpleFOC.h>

// TODO: motor instance 
// TODO: driver instance

// TODO: sensor instance

// TODO: current sense instance

// instantiate the commander
Commander command = Commander(Serial);
void doMotor(char* cmd) { command.motor(&motor, cmd); }

void setup() { 
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);

  // TODO: driver config
  // init the sensor
  sensor.init()
  // link the motor to the sensor
  motor.linkSensor(&sensor);

  // TODO: driver config
  // init the driver
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link driver
  motor.linkDriver(&driver);
  // link driver to cs
  current_sense.linkDriver(&driver);

  // TODO: current sense config
  // current sense init
  if(!current_sense.init()){
    Serial.println("Current sense init failed!");
    return;
  }
  // link the current sense to the motor
  motor.linkCurrentSense(&current_sense);

  // TODO: motor conf

  // set motion control loop to be used
  motor.torque_controller = TorqueControlType::voltage;
  motor.controller = MotionControlType::torque;

  // use monitoring with serial
  // comment out if not needed
  motor.useMonitoring(Serial);
  motor.monitor_downsampling = 100; // set downsampling can be even more > 100
  motor.monitor_variables = _MON_CURR_Q | _MON_CURR_D; // set monitoring of d and q currents

  // initialize motor
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }
  // align sensor and start FOC
  if(!motor.initFOC()){
    Serial.println("FOC init failed!");
    return;
  }

  // set the initial motor target
  motor.target = 2; // Volts 

  // add target command M
  command.add('M', doMotor, "Motor");

  Serial.println(F("Motor ready."));
  Serial.println(F("Set the target using serial terminal and command M:"));
  _delay(1000);
}

void loop() {
  // main FOC algorithm function
  motor.loopFOC();

  // Motion control function
  motor.move();

  // display the currents
  motor.monitor();
  // user communication
  command.run();
}
```
</div>

<div class="current current-bldc hide" markdown="1">

ä»¥ä¸‹æ˜¯ä½¿ç”¨ **3PWM driver**ã€**Encoder** å’Œ **Inline Current Sensing** çš„ **BLDC motor** ç¤ºä¾‹ä»£ç ã€‚

```cpp
#include <SimpleFOC.h>

// BLDC motor & driver instance
BLDCMotor motor = BLDCMotor(11);
BLDCDriver3PWM driver = BLDCDriver3PWM(9, 5, 6, 8);

// encoder instance
Encoder encoder = Encoder(2, 3, 500);
// channel A and B callbacks
void doA(){encoder.handleA();}
void doB(){encoder.handleB();}

// current sensor
InlineCurrentSense current_sense = InlineCurrentSense(0.01, 50.0, A0, A2);

// instantiate the commander
Commander command = Commander(Serial);
void doMotor(char* cmd) { command.motor(&motor, cmd); }

void setup() { 
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // initialize encoder sensor hardware
  encoder.init();
  encoder.enableInterrupts(doA, doB); 
  // link the motor to the sensor
  motor.linkSensor(&encoder);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // driver init
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link driver
  motor.linkDriver(&driver);
  // link driver to cs
  current_sense.linkDriver(&driver);

  // current sense init hardware
  if(!current_sense.init()){
    Serial.println("Current sense init failed!");
    return;
  }
  // link the current sense to the motor
  motor.linkCurrentSense(&current_sense);

  // aligning voltage
  motor.voltage_sensor_align = 5;

  // set motion control loop to be used
  motor.torque_controller = TorqueControlType::voltage;
  motor.controller = MotionControlType::torque;

  // use monitoring with serial
  // comment out if not needed
  motor.useMonitoring(Serial);
  motor.monitor_downsampling = 100; // set downsampling can be even more > 100
  motor.monitor_variables = _MON_CURR_Q | _MON_CURR_D; // set monitoring of d and q currents

  // initialize motor
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }
  // align sensor and start FOC
  if(!motor.initFOC()){
    Serial.println("FOC init failed!");
    return;
  }

  // set the initial motor target
  motor.target = 2; // Volts 

  // add target command M
  command.add('M', doMotor, "Motor");

  Serial.println(F("Motor ready."));
  Serial.println(F("Set the target using serial terminal and command M:"));
  _delay(1000);
}

void loop() {
  // main FOC algorithm function
  motor.loopFOC();

  // Motion control function
  motor.move();

  // display the currents
  motor.monitor();
  // user communication
  command.run();
}
```
</div>

<div class="current current-stepper hide" markdown="1">

ä»¥ä¸‹æ˜¯ä½¿ç”¨**2PWM driver**, **Encoder** å’Œ**Inline Current Sensing**çš„**Stepper motor**ç¤ºä¾‹ä»£ç ã€‚

```cpp
#include <SimpleFOC.h>

// Stepper motor & driver instance
StepperMotor motor = StepperMotor(50);
StepperDriver2PWM driver = StepperDriver2PWM(9, 5, 6, 8);

// encoder instance
Encoder encoder = Encoder(2, 3, 500);
// channel A and B callbacks
void doA(){encoder.handleA();}
void doB(){encoder.handleB();}

// current sensor
InlineCurrentSense current_sense = InlineCurrentSense(0.01, 50.0, A0, A2);

// instantiate the commander
Commander command = Commander(Serial);
void doMotor(char* cmd) { command.motor(&motor, cmd); }

void setup() { 
  
  // use monitoring with serial 
  Serial.begin(115200);
  // enable more verbose output for debugging
  // comment out if not needed
  SimpleFOCDebug::enable(&Serial);
  
  // initialize encoder sensor hardware
  encoder.init();
  encoder.enableInterrupts(doA, doB); 
  // link the motor to the sensor
  motor.linkSensor(&encoder);

  // driver config
  // power supply voltage [V]
  driver.voltage_power_supply = 12;
  // driver init
  if(!driver.init()){
    Serial.println("Driver init failed!");
    return;
  }
  // link driver
  motor.linkDriver(&driver);
  // link driver to cs
  current_sense.linkDriver(&driver);

  // current sense init hardware
  if(!current_sense.init()){
    Serial.println("Current sense init failed!");
    return;
  }
  // link the current sense to the motor
  motor.linkCurrentSense(&current_sense);

  // aligning voltage
  motor.voltage_sensor_align = 5;

  // set motion control loop to be used
  motor.torque_controller = TorqueControlType::voltage;
  motor.controller = MotionControlType::torque;

  // use monitoring with serial
  // comment out if not needed
  motor.useMonitoring(Serial);
  motor.monitor_downsampling = 100; // set downsampling can be even more > 100
  motor.monitor_variables = _MON_CURR_Q | _MON_CURR_D; // set monitoring of d and q currents

  // initialize motor
  if(!motor.init()){
    Serial.println("Motor init failed!");
    return;
  }
  // align sensor and start FOC
  if(!motor.initFOC()){
    Serial.println("FOC init failed!");
    return;
  }

  // set the initial motor target
  motor.target = 2; // Volts 

  // add target command M
  command.add('M', doMotor, "Motor");

  Serial.println(F("Motor ready."));
  Serial.println(F("Set the target using serial terminal and command M:"));
  _delay(1000);
}

void loop() {
  // main FOC algorithm function
  motor.loopFOC();

  // Motion control function
  motor.move();

  // display the currents
  motor.monitor();
  // user communication
  command.run();
}
```
</div>

è¿™ä»½ç¤ºä¾‹ä»£ç ä¸æ­¥éª¤ 3 çš„ä»£ç å‡ ä¹å®Œå…¨ç›¸åŒï¼Œå› æ­¤ä½ åœ¨é…ç½®ç”µæœºã€ä¼ æ„Ÿå™¨å’Œé©±åŠ¨å™¨æ—¶åº”è¯¥ä¸ä¼šé‡åˆ°å¤ªå¤šå›°éš¾ã€‚æœ¬æ­¥éª¤ä¸­ï¼Œä½ å°†æµ‹è¯•ç”µæµæ£€æµ‹åŠŸèƒ½æ˜¯å¦æ­£å¸¸å·¥ä½œã€‚è°ƒç”¨ `motor.monitor()` å‡½æ•°æ—¶ï¼Œç³»ç»Ÿä¼šè¯»å–ç”µæµæ£€æµ‹æ•°æ®ï¼Œå¹¶å°† d è½´ç”µæµå’Œ q è½´ç”µæµæ‰“å°åˆ°ä¸²å£ç»ˆç«¯ã€‚ä½ å¯ä»¥æ‰“å¼€ä¸²å£ç»˜å›¾å™¨ï¼ˆSerial Plotterï¼‰æ¥å¯è§†åŒ–è¿™äº›æ•°æ®ã€‚

<blockquote class="info" markdown="1"> <p class="heading">â˜‘ï¸ ç®€å•æµ‹è¯•</p> 

1. è‹¥æ‰€æœ‰è¿æ¥æ­£å¸¸ï¼Œç”µæœºåº”èƒ½æ—‹è½¬ï¼Œä¸”ä½ å¯é€šè¿‡ä¸²å£ç»ˆç«¯ï¼ˆè¾“å…¥å‘½ä»¤ `M`ï¼‰è®¾ç½®ç›®æ ‡ç”µå‹ã€‚
2. ç”¨æ‰‹æ¡ä½ç”µæœºï¼Œè®¾ç½®ä¸åŒçš„ç›®æ ‡ç”µå‹/ç”µæµå€¼ã€‚ç¡®ä¿ç”µæœºé™æ­¢æ—¶ï¼Œd è½´ç”µæµéå¸¸æ¥è¿‘ 0ï¼›åŒæ—¶ç¡®ä¿ q è½´ç”µæµä¸ä½ è®¾ç½®çš„ç”µæœºç”µå‹æˆæ­£æ¯”ã€‚<br>
3. è®©ç”µæœºè‡ªç”±æ—‹è½¬ï¼Œè§‚å¯Ÿ d è½´ç”µæµå’Œ q è½´ç”µæµæ˜¯å¦é™è‡³ä½äºç”µæœºé™æ­¢æ—¶çš„æ•°å€¼ã€‚åŒæ—¶å°è¯•è§‚å¯Ÿï¼šä½è½¬é€Ÿæ—¶ d è½´ç”µæµå‡ ä¹ä¸º 0ï¼Œä¸”ä¼šéšç€ç”µæœºè½¬é€Ÿçš„å¢åŠ è€Œæˆæ¯”ä¾‹ä¸Šå‡ã€‚
</blockquote>

å¦‚æœç”µæœºæœªæ—‹è½¬ï¼Œè¯·é€šè¿‡æŸ¥çœ‹ä¸²å£ç»ˆç«¯è¾“å‡ºæ¥ç¡®è®¤ç”µæœºå’Œé©±åŠ¨å™¨å·²æˆåŠŸåˆå§‹åŒ–ï¼ˆç¡®ä¿èƒ½çœ‹åˆ° ` Motor ready!`ï¼Œè€Œé` Motor init failed!`ã€`Driver init failed!` æˆ– `Current sense init failed!`ï¼‰ã€‚æˆ‘ä»¬çš„è°ƒè¯•ç•Œé¢ä¼šåœ¨é©±åŠ¨å™¨ã€ç”µæµæ£€æµ‹å’Œç”µæœºåˆå§‹åŒ–è¿‡ç¨‹ä¸­è¾“å‡ºå¤§é‡ä¿¡æ¯ï¼Œå¯å¸®åŠ©ä½ æ’æŸ¥è£…ç½®å¯èƒ½å­˜åœ¨çš„é—®é¢˜ã€‚è‹¥ç”µæµæ£€æµ‹åŠŸèƒ½å‡ºç°é—®é¢˜ï¼Œè¯·æŸ¥é˜… [ç”µæµæ£€æµ‹æ–‡æ¡£](current_sense)ï¼Œäº†è§£æ”¯æŒçš„ä¼ æ„Ÿå™¨åŠæ‰€æœ‰é…ç½®å‚æ•°ã€‚

å¯èƒ½å‡ºç°çš„æœ€å¸¸è§é—®é¢˜å¦‚ä¸‹ï¼š
- ç”µæµæ£€æµ‹å¼•è„šå¹¶é ADC å¼•è„š â€”â€” æ›´å¤šä¿¡æ¯è¯·å‚è§ [ADC å¼•è„šé€‰æ‹©æŒ‡å—](choosing_adc_pins)ã€‚
- é©±åŠ¨å™¨æœªä¸ç”µæµæ£€æµ‹æ¨¡å—å…³è” â€”â€” ç¡®ä¿ä»£ç ä¸­åŒ…å« `current_sense.linkDriver(&driver)`; è¿™ä¸€è¡Œã€‚
- PWM å®šæ—¶å™¨ä¸ ADC æ— æ³•åŒæ­¥ â€”â€” æ›´å¤šä¿¡æ¯è¯·å‚è§ [PWM å¼•è„šé€‰æ‹©æŒ‡å—](choosing_pwm_pins)ã€‚
- ç”µæµæ£€æµ‹å¯¹é½å¤±è´¥ â€”â€” éªŒè¯ `motor.initFOC()` å‡½æ•°çš„è°ƒè¯•è¾“å‡ºï¼Œå¹¶å°è¯•ä¿®æ”¹ `motor.voltage_sensor_align `çš„å€¼ã€‚

## æ­¥éª¤ 6. åŸºäºç”µæµæ£€æµ‹çš„å®Œæ•´ FOC è¿åŠ¨æ§åˆ¶ï¼ˆå¦‚æ”¯æŒï¼‰

å½“ä½ å·²å®Œæˆç”µæœºã€ä½ç½®ä¼ æ„Ÿå™¨ã€é©±åŠ¨å™¨å’Œç”µæµæ£€æµ‹æ¨¡å—çš„é…ç½®ä¸æµ‹è¯•åï¼Œå³å¯å°è¯•çœŸæ­£çš„ç£åœºå®šå‘æ§åˆ¶ï¼ˆField Oriented Control, FOCï¼‰ã€‚

- è¦ä»åŸºäºç”µå‹çš„è½¬çŸ©æ§åˆ¶åˆ‡æ¢åˆ°åŸºäºç”µæµçš„è½¬çŸ©æ§åˆ¶ï¼Œåªéœ€å°† `motor.torque_controller` å˜é‡æ”¹ä¸º` TorqueControlType::foc_current`ï¼Œå¹¶å°† `motor.controller` å˜é‡è®¾ç½®ä¸º` MotionControlType::torque`ã€‚
- å¦ä¸€ç§åˆ‡æ¢åˆ°ç”µæµæ§åˆ¶æ¨¡å¼çš„æ–¹æ³•æ˜¯é€šè¿‡ `Commander` æ¥å£å€ŸåŠ©ä¸²å£ç»ˆç«¯æ“ä½œï¼š
  - è¦å¯åŠ¨ç”µæµæ§åˆ¶æ¨¡å¼ï¼Œåœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `MT2` å¹¶æŒ‰å›è½¦ã€‚
    - è¦è®¾ç½®ç›®æ ‡ç”µæµï¼Œåœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `M0.5`ï¼ˆå•ä½ï¼šå®‰åŸ¹ï¼‰å¹¶æŒ‰å›è½¦ï¼Œè¿™ä¼šå°†ç›®æ ‡ç”µæµè®¾ç½®ä¸º 0.5 å®‰åŸ¹ã€‚
    - è¦åœæ­¢ç”µæœºï¼Œåœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `M0` å¹¶æŒ‰å›è½¦ï¼Œè¿™ä¼šå°†ç›®æ ‡ç”µæµè®¾ç½®ä¸º 0ã€‚
  - è¦è¿”å›ç”µå‹æ§åˆ¶æ¨¡å¼ï¼Œåœ¨ä¸²å£ç»ˆç«¯ä¸­è¾“å…¥ `MT0` å¹¶æŒ‰å›è½¦ã€‚
  - æœ‰å…³ `Commander` æ¥å£çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚è§ [Commander æ–‡æ¡£](commander_interface)ã€‚


<blockquote class="info"> <p class="heading">â˜‘ï¸ ç®€å•æµ‹è¯•</p> 

1. å°†ç›®æ ‡ç”µæµè®¾ç½®ä¸º <b>0 å®‰åŸ¹</b>ï¼Œå°è¯•ç”¨æ‰‹è½¬åŠ¨ç”µæœºï¼Œç¡®ä¿æ„Ÿè§‰å®Œå…¨æ— é˜»åŠ›ï¼Œå°±åƒç”µæœºæœªå¯ç”¨ä¸€æ ·ã€‚

2. ç„¶åå°è¯•è®¾ç½®ä¸€ä¸ªè¾ƒå°çš„ç”µæµå€¼ï¼ˆ<b><0.5 å®‰åŸ¹</b>ï¼‰ï¼Œè§‚å¯Ÿæ˜¯å¦èƒ½æ„Ÿå—åˆ°ç”µæœºå¯¹äººæ‰‹æ–½åŠ çš„ä½œç”¨åŠ›ã€‚è‹¥èƒ½æ„Ÿå—åˆ°ï¼Œåˆ™è¯´æ˜ä¸€åˆ‡å°±ç»ªï¼Œå¯ä»¥è¿›è¡Œåç»­æ“ä½œï¼
</blockquote>


ç”µæµæ§åˆ¶æ¨¡å¼æœ‰é¢å¤–çš„é…ç½®å‚æ•°ï¼Œè¦ä½¿ç”µæœºè¿è¡Œå¹³ç¨³ä¸”ç¨³å®šï¼Œå¯èƒ½éœ€è¦é…ç½®è¿™äº›å‚æ•°ã€‚æœ‰å…³è¿™äº›å‚æ•°çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚è§ [è½¬çŸ©æ§åˆ¶æ–‡æ¡£](torque_control)ã€‚


ä¸è¿‡ï¼Œæœ€é‡è¦çš„æŠ€å·§ä»æ˜¯ä½¿ç”¨ [Commander æ¥å£](commander_interface) æ¥è°ƒæ•´è½¬çŸ©æ§åˆ¶çš„ PID æ§åˆ¶å™¨å’Œä½é€šæ»¤æ³¢å™¨å‚æ•°ã€‚é€šè¿‡è¿™ç§æ–¹å¼ï¼Œä½ å¯ä»¥å®æ—¶ä¿®æ”¹å’Œæµ‹è¯•æ§åˆ¶å™¨å‚æ•°ï¼Œè§‚å¯Ÿå…¶æ•ˆæœã€‚æ»¡æ„åï¼Œå¯å°†è¿™äº›å‚æ•°å†™å…¥ä»£ç ï¼Œä¸å†ä½¿ç”¨ Commander æ¥å£ã€‚

### æ­¥éª¤ 6.1. é€Ÿåº¦å’Œè§’åº¦æ§åˆ¶

å½“ç”µæµæ§åˆ¶åŠŸèƒ½æ­£å¸¸å·¥ä½œåï¼Œä½ å¯ä»¥ç»§ç»­è¿›è¡Œé€Ÿåº¦å’Œè§’åº¦è¿åŠ¨æ§åˆ¶ã€‚
- è¦ä»ç”µæµæ§åˆ¶åˆ‡æ¢åˆ°é€Ÿåº¦æ§åˆ¶ï¼Œåªéœ€å°† `motor.controller` å˜é‡æ”¹ä¸º `MotionControlType::velocity`ï¼Œå¹¶å°† `motor.target` è®¾ç½®ä¸ºæ‰€éœ€é€Ÿåº¦ï¼ˆå•ä½ï¼šå¼§åº¦ / ç§’ RAD/sï¼‰ã€‚
- è¦ä»ç”µæµæ§åˆ¶åˆ‡æ¢åˆ°è§’åº¦æ§åˆ¶ï¼Œåªéœ€å°† `motor.controller` å˜é‡æ”¹ä¸º `MotionControlType::angle`ï¼Œå¹¶å°† `motor.target` è®¾ç½®ä¸ºæ‰€éœ€è§’åº¦ï¼ˆå•ä½ï¼šå¼§åº¦ RADï¼‰ã€‚
- ä½ ä¹Ÿå¯ä»¥ä½¿ç”¨ `Commander` æ¥å£ï¼Œé€šè¿‡ `MC1` å’Œ `MC2` å‘½ä»¤åˆ‡æ¢åˆ°é€Ÿåº¦å’Œè§’åº¦æ§åˆ¶æ¨¡å¼ã€‚

é€Ÿåº¦å’Œè§’åº¦è¿åŠ¨æ§åˆ¶æ¨¡å¼æœ‰é¢å¤–çš„é…ç½®å‚æ•°ï¼Œè¦ä½¿ç”µæœºè¿è¡Œå¹³ç¨³ä¸”ç¨³å®šï¼Œå¯èƒ½éœ€è¦é…ç½®è¿™äº›å‚æ•°ã€‚æœ‰å…³è¿™äº›å‚æ•°çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚è§ [è¿åŠ¨æ§åˆ¶æ–‡æ¡£](closed_loop_motion_control)ã€‚

